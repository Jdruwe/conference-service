buildscript {
    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "http://dl.bintray.com/typesafe/maven-releases" }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.3.RELEASE")
        classpath "io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE"
    }
}

subprojects {
    apply plugin: "java-library"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "pmd"
    apply plugin: "checkstyle"
    apply plugin: "jacoco"

    group = "be.xplore"
    version = "0.0.1"
    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    configurations { codacy }

    checkstyle {
        toolVersion = "8.18"
        configFile = file("config/checkstyle/checkstyle.xml")
    }

    pmd {
        ruleSets = []
        ruleSetFiles = files("config/pmd/ruleset.xml")
    }

    jacoco {
        toolVersion = "0.8.3"
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    task sendCoverageToCodacy(type: JavaExec, dependsOn: jacocoTestReport) {
        main = "com.codacy.CodacyCoverageReporter"
        classpath = configurations.codacy
        args = [
                "report",
                "-l",
                "Java",
                "-r",
                "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        ]
    }

    test.finalizedBy jacocoTestReport
    jacocoTestReport.finalizedBy sendCoverageToCodacy
}
